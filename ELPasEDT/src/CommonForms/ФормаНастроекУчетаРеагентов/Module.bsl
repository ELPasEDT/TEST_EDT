
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиУчетаРеагентовСрезПоследних.Период КАК Период
	|ИЗ
	|	РегистрСведений.НастройкиУчетаРеагентов.СрезПоследних КАК НастройкиУчетаРеагентовСрезПоследних");
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Период = Выборка.Период;
		ПрочитатьДанные(Период);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанные(Период = '00010101')
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиУчетаРеагентовСрезПоследних.БазоваяЕдиницаИзмеренияПлощади КАК БазоваяЕдиницаИзмеренияПлощади,
	|	НастройкиУчетаРеагентовСрезПоследних.БазоваяЕдиницаИзмеренияУровня КАК БазоваяЕдиницаИзмеренияУровня
	|ИЗ
	|	РегистрСведений.НастройкиУчетаРеагентов.СрезПоследних(&Период, ) КАК НастройкиУчетаРеагентовСрезПоследних");
	Запрос.УстановитьПараметр("Период", Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		БазоваяЕдиницаИзмеренияПлощади = Выборка.БазоваяЕдиницаИзмеренияПлощади;
		БазоваяЕдиницаИзмеренияУровня  = Выборка.БазоваяЕдиницаИзмеренияУровня;
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиУчетаРеагентовПоОрганизациямСрезПоследних.Организация КАК Организация,
	|	НастройкиУчетаРеагентовПоОрганизациямСрезПоследних.КонтрольСмены КАК КонтрольСмены,
	|	НастройкиУчетаРеагентовПоОрганизациямСрезПоследних.КонтрольОстатков КАК КонтрольОстатков
	|ИЗ
	|	РегистрСведений.НастройкиУчетаРеагентовПоОрганизациям.СрезПоследних(&Период, ) КАК НастройкиУчетаРеагентовПоОрганизациямСрезПоследних";
	НастройкиПоОрганизациям.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьНаСервере();
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	МЗ = РегистрыСведений.НастройкиУчетаРеагентов.СоздатьМенеджерЗаписи();
	МЗ.Период = Период;
	МЗ.БазоваяЕдиницаИзмеренияПлощади = БазоваяЕдиницаИзмеренияПлощади;
	МЗ.БазоваяЕдиницаИзмеренияУровня  = БазоваяЕдиницаИзмеренияУровня;
	МЗ.Записать();
	
	Для Каждого Стр Из НастройкиПоОрганизациям Цикл
	
		МЗ = РегистрыСведений.НастройкиУчетаРеагентовПоОрганизациям.СоздатьМенеджерЗаписи();
		МЗ.Период = Период;
		МЗ.Организация = Стр.Организация;
		МЗ.КонтрольСмены = Стр.КонтрольСмены;
		МЗ.КонтрольОстатков = Стр.КонтрольОстатков;
		МЗ.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура История(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПриВыбореПериода", ЭтотОбъект);
	
	ПоказатьВыборИзСписка(Оповещение, ПолучитьСписокПериодов());
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореПериода(ВыбранноеЗначение, ДопПараметры) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Период = ВыбранноеЗначение.Значение;
	ПрочитатьДанные(ВыбранноеЗначение.Значение);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокПериодов()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиУчетаРеагентов.Период КАК Период
	|ИЗ
	|	РегистрСведений.НастройкиУчетаРеагентов КАК НастройкиУчетаРеагентов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период");
	
	Список = Новый СписокЗначений;
	Список.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Период"));
	
	Возврат Список;
	
КонецФункции

&НаСервере
Процедура УдалитьНастройкуНаСервере()
	
	МЗ = РегистрыСведений.НастройкиУчетаРеагентов.СоздатьМенеджерЗаписи();
	МЗ.Период = Период;
	
	МЗ.Удалить();
	
	Для Каждого Стр Из НастройкиПоОрганизациям Цикл
	
		МЗ = РегистрыСведений.НастройкиУчетаРеагентовПоОрганизациям.СоздатьМенеджерЗаписи();
		МЗ.Период = Период;
		МЗ.Организация = Стр.Организация;
		
		МЗ.Удалить();
		
	КонецЦикла;
	
	Период                         = Неопределено;
	БазоваяЕдиницаИзмеренияПлощади = Неопределено;
	БазоваяЕдиницаИзмеренияУровня  = Неопределено;
	
	НастройкиПоОрганизациям.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНастройку(Команда)
	УдалитьНастройкуНаСервере();
КонецПроцедуры
