
Процедура ВыполнитьКонтрольОстатков(Параметры, Отказ) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиУчетаРеагентовПоОрганизациямСрезПоследних.КонтрольОстатков КАК КонтрольОстатков
	|ИЗ
	|	РегистрСведений.НастройкиУчетаРеагентовПоОрганизациям.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ(&Подразделение КАК Справочник.Подразделения).Организация) КАК НастройкиУчетаРеагентовПоОрганизациямСрезПоследних");
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	Запрос.УстановитьПараметр("Период",        Параметры.Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Или Не Выборка.КонтрольОстатков Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РеагентыВПодразделенияхОстатки.КоличествоОстаток КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.РеагентыВПодразделениях.Остатки(
	|			&Период,
	|			ВидРеагента = &ВидРеагента
	|				И Партия = &Партия
	|				И Подразделение = &Подразделение
	|				И Реагент = &Реагент) КАК РеагентыВПодразделенияхОстатки";
	Запрос.УстановитьПараметр("ВидРеагента",   Параметры.ВидРеагента);
	Запрос.УстановитьПараметр("Партия",        Параметры.Партия);
	Запрос.УстановитьПараметр("Реагент",       Параметры.Реагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() И Выборка.КоличествоОстаток < 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Операция не выполнена! Ндостаточно %1%2 реагента", -Выборка.КоличествоОстаток, Параметры.Партия.ЕдиницаИзмеренияКоличества);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли;
	
КонецПроцедуры