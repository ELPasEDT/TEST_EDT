
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТолькоАктивные = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтбор();
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоАктивные(Команда)
	
	ТолькоАктивные = Не ТолькоАктивные;
	УстановитьОтбор();
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтбор()
	
	ЭлементОтбора = Неопределено;
	
	Поле = Новый ПолеКомпоновкиДанных("ЭлементАктивен");
	
	Для Каждого Элемент Из  Список.Отбор.Элементы Цикл
		Если Элемент.ЛевоеЗначение = Поле Тогда
			ЭлементОтбора = Элемент;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭлементОтбора = Неопределено Тогда
		ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Поле;
	КонецЕсли;
	
	ЭлементОтбора.Использование = ТолькоАктивные;
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	Элементы.ТолькоАктивные.Пометка = ТолькоАктивные;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	
	Элементы.ЭлементАктивен.Видимость = Не ТолькоАктивные;
	
КонецПроцедуры