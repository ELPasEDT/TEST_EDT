
&НаКлиенте
Процедура РеагентПриИзменении(Элемент)
	РеагентПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура РеагентПриИзмененииНаСервере()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запись.ВидРеагента = Запись.Реагент.ВидРеагента;
	
	БазоваяЕдиницаИзмеренияОбъема = ПредопределенноеЗначение("Справочник.КлассификаторЕдиницИзмерения.ПустаяСсылка");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	БазовыеЕдиницыИзмеренияРеагентов.БазоваяЕдиницаИзмеренияОбъема КАК БазоваяЕдиницаИзмеренияОбъема
	|ИЗ
	|	РегистрСведений.БазовыеЕдиницыИзмеренияРеагентов КАК БазовыеЕдиницыИзмеренияРеагентов
	|ГДЕ
	|	БазовыеЕдиницыИзмеренияРеагентов.ВидРеагента = &ВидРеагента");
	Запрос.УстановитьПараметр("ВидРеагента", Запись.ВидРеагента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		БазоваяЕдиницаИзмеренияОбъема = Выборка.БазоваяЕдиницаИзмеренияОбъема;
	КонецЕсли;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПараметровАРМУчетаРеагентов.ЕдиницаИзмеренияКонцентрацииРаствора КАК ЕдиницаИзмеренияКонцентрации
	|ИЗ
	|	РегистрСведений.НастройкиПараметровАРМУчетаРеагентов КАК НастройкиПараметровАРМУчетаРеагентов
	|ГДЕ
	|	НастройкиПараметровАРМУчетаРеагентов.Организация = &Организация
	|	И НастройкиПараметровАРМУчетаРеагентов.Реагент = &Реагент";
	Запрос.УстановитьПараметр("Организация", Запись.Организация);
	Запрос.УстановитьПараметр("Реагент", Запись.Реагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЕдиницаИзмеренияКонцентрации = Выборка.ЕдиницаИзмеренияКонцентрации;
	Иначе
		ЕдиницаИзмеренияКонцентрации = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиУчетаРеагентовСрезПоследних.БазоваяЕдиницаИзмеренияПлощади КАК БазоваяЕдиницаИзмеренияПлощади,
	|	НастройкиУчетаРеагентовСрезПоследних.БазоваяЕдиницаИзмеренияУровня КАК БазоваяЕдиницаИзмеренияУровня
	|ИЗ
	|	РегистрСведений.НастройкиУчетаРеагентов.СрезПоследних КАК НастройкиУчетаРеагентовСрезПоследних");
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		БазоваяЕдиницаИзмеренияПлощади = Выборка.БазоваяЕдиницаИзмеренияПлощади;
		БазоваяЕдиницаИзмеренияУровня = Выборка.БазоваяЕдиницаИзмеренияУровня;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	РеагентПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОборудованиеПриИзмененииНаСервере()
	
	Запись.Организация   = Запись.Оборудование.Организация;
	Запись.Подразделение = Запись.Оборудование.Подразделение;
	
	Если Не ЗначениеЗаполнено(Запись.СостояниеОборудования) Тогда
		Запись.СостояниеОборудования = Перечисления.СостоянияОборудования.ВРаботе;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборудованиеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.Оборудование) Тогда
		ОборудованиеПриИзмененииНаСервере();
	Иначе
		Запись.Организация   = Неопределено;
		Запись.Подразделение = Неопределено;
	КонецЕсли;
КонецПроцедуры
