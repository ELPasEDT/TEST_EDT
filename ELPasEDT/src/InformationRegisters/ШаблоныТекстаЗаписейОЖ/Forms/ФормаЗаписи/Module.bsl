
&НаКлиенте
Процедура СобытиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = ПолучитьСписокСобытий();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписокСобытий() 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если Запись.ВидЗаписи = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СписокСобытий = Новый СписокЗначений();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВидыЗаписейСобытияПоПодразделениям.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТ_Документ
		|ИЗ
		|	РегистрСведений.ВидыЗаписейСобытияПоПодразделениям КАК ВидыЗаписейСобытияПоПодразделениям
		|ГДЕ
		|	ВидыЗаписейСобытияПоПодразделениям.Подразделение = &Подразделение
		|	И ВидыЗаписейСобытияПоПодразделениям.ВидЗаписи = &ВидЗаписи
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидыЗаписейСобытияПоПодразделениям.Период УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗакреплениеВидаЗаписиИСобытийЗаПодразделениемСписокСобытий.Событие КАК Событие
		|ИЗ
		|	Документ.ЗакреплениеВидаЗаписиИСобытийЗаПодразделением.СписокСобытий КАК ЗакреплениеВидаЗаписиИСобытийЗаПодразделениемСписокСобытий
		|ГДЕ
		|	ЗакреплениеВидаЗаписиИСобытийЗаПодразделениемСписокСобытий.Ссылка.Ссылка В
		|			(ВЫБРАТЬ
		|				ВТ_Документ.Регистратор КАК Регистратор
		|			ИЗ
		|				ВТ_Документ КАК ВТ_Документ)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗакреплениеВидаЗаписиИСобытийЗаПодразделениемСписокСобытий.Порядок";
	
	Запрос.УстановитьПараметр("ВидЗаписи", Запись.ВидЗаписи);
	Запрос.УстановитьПараметр("Подразделение", Запись.Подразделение);  	
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Пока Результат.Следующий() Цикл
		
		СписокСобытий.Добавить(Результат.Событие);
		
	КонецЦикла;
	
	Возврат СписокСобытий;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запись.Подразделение = ПараметрыСеанса.ТекущийПользователь.Подразделение;
	Запись.Организация = Запись.Подразделение.Организация;
	ЗначениеКопирования = Параметры.ЗначениеКопирования;
	Если ЗначениеЗаполнено(ЗначениеКопирования) Тогда
		Запись.НомерШаблона = ВернутьНомерШаблона(ЗначениеКопирования.ВидЗаписи, ЗначениеКопирования.Организация, ЗначениеКопирования.Подразделение, ЗначениеКопирования.Событие);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьНомерШаблона(ВидЗаписи, Организация, Подразделение, Событие)   	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ШаблоныТекстаЗаписейОЖ.НомерШаблона КАК НомерШаблона
		|ИЗ
		|	РегистрСведений.ШаблоныТекстаЗаписейОЖ КАК ШаблоныТекстаЗаписейОЖ
		|ГДЕ
		|	ШаблоныТекстаЗаписейОЖ.ВидЗаписи = &ВидЗаписи
		|	И ШаблоныТекстаЗаписейОЖ.Событие = &Событие
		|	И ШаблоныТекстаЗаписейОЖ.Подразделение = &Подразделение
		|	И ШаблоныТекстаЗаписейОЖ.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерШаблона УБЫВ";
	
	Запрос.УстановитьПараметр("ВидЗаписи", ВидЗаписи);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Событие", Событие);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		Возврат Результат.НомерШаблона + 1;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура СобытиеПриИзменении(Элемент)
	
	Если Не СобытиеРазрешено() Тогда
		Запись.Событие = Неопределено;
		Элемент = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СобытиеРазрешено()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Поле1
		|ИЗ
		|	РегистрСведений.ВидыЗаписейСобытияПоПодразделениям.СрезПоследних(
		|			,
		|			Подразделение = &Подразделение
		|				И ВидЗаписи = &ВидЗаписи
		|				И Событие = &Событие) КАК ВидыЗаписейСобытияПоПодразделениямСрезПоследних";
	
	Запрос.УстановитьПараметр("ВидЗаписи", Запись.ВидЗаписи);
	Запрос.УстановитьПараметр("Событие", Запись.Событие);
	Запрос.УстановитьПараметр("Подразделение", Запись.Подразделение);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Возврат Результат.Следующий();
	
КонецФункции

&НаКлиенте
Процедура ВидЗаписиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Настройки = ПолучитьНастройкиОткрытияФормыВыбораВидаЗаписи(); 
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ФиксированныеНастройки", Настройки);
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("Заголовок", "Виды записей");
	ПараметрыФормы.Вставить("Подразделение", Запись.Подразделение);
	
	ОткрытьФорму("Справочник.ВидыЗаписей.Форма.ФормаВыбораВидыЗаписи", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьНастройкиОткрытияФормыВыбораВидаЗаписи() Экспорт
	
	Настройки = Новый НастройкиКомпоновкиДанных;
	
	Список = Новый СписокЗначений;
	Список.Добавить(ПредопределенноеЗначение("Справочник.ВидыЗаписей.ПередачаСмены"));
	Список.Добавить(ПредопределенноеЗначение("Справочник.ВидыЗаписей.РаспоряжениеНСС"));
    ЭлементНастройки = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    ЭлементНастройки.Использование = Истина;
    ЭлементНастройки.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Ссылка");
    ЭлементНастройки.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
    ЭлементНастройки.ПравоеЗначение = Список;
    ЭлементНастройки.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный;
	
	Возврат Настройки;
	
КонецФункции

&НаКлиенте
Процедура ВидЗаписиИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ВидЗаписиПриИзменении(Элемент)
	
	Если Запись.ВидЗаписи = ПредопределенноеЗначение("Справочник.ВидыЗаписей.ПередачаСмены") Тогда
		Запись.ВидЗаписи = Неопределено;
	КонецЕсли; 
	
	Если Не ВидЗаписиРазрешен() Тогда
		Запись.ВидЗаписи = Неопределено;
		Элемент = Неопределено;
	КонецЕсли;
	
	Запись.Событие = Неопределено;	
	
КонецПроцедуры

&НаСервере
Функция ВидЗаписиРазрешен()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Поле1
		|ИЗ
		|	РегистрСведений.ВидыЗаписейСобытияПоПодразделениям.СрезПоследних(
		|			,
		|			Подразделение = &Подразделение
		|				И ВидЗаписи = &ВидЗаписи) КАК ВидыЗаписейСобытияПоПодразделениямСрезПоследних";
	
	Запрос.УстановитьПараметр("ВидЗаписи", Запись.ВидЗаписи);
	Запрос.УстановитьПараметр("Подразделение", Запись.Подразделение);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Возврат Результат.Следующий();
	
КонецФункции

















