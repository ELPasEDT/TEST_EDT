
&НаКлиенте
Процедура СписокОборудованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура("Оборудование", ВыбраннаяСтрока);
	
	Если ВыбранныеЭлементы.НайтиСтроки(Отбор).Количество() = 0 Тогда
		ВыбранныеЭлементы.Добавить().Оборудование = ВыбраннаяСтрока;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Источник.ВидОборудования) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Для Источника не указан Вид оборудования");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗначенияРеквизитовПаспортов.Период КАК Период,
	|	ЗначенияРеквизитовПаспортов.Объект КАК Объект,
	|	ЗначенияРеквизитовПаспортов.Реквизит КАК Реквизит,
	|	ЗначенияРеквизитовПаспортов.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ЗначенияРеквизитовПаспортов КАК ЗначенияРеквизитовПаспортов
	|ГДЕ
	|	ЗначенияРеквизитовПаспортов.Объект = &Объект");
	Запрос.УстановитьПараметр("Объект", Источник);
	ВыборкаИсточник = Запрос.Выполнить().Выбрать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗначенияРеквизитовПаспортов.Период КАК Период,
	|	ЗначенияРеквизитовПаспортов.Объект КАК Объект,
	|	ЗначенияРеквизитовПаспортов.Реквизит КАК Реквизит
	|ИЗ
	|	РегистрСведений.ЗначенияРеквизитовПаспортов КАК ЗначенияРеквизитовПаспортов
	|ГДЕ
	|	ЗначенияРеквизитовПаспортов.Объект = &Объект";
	
	Для Каждого Элемент Из ВыбранныеЭлементы Цикл
		
		СпрОбъект = Элемент.Оборудование.ПолучитьОбъект();
		СпрОбъект.ВидОборудования = Источник.ВидОборудования;
		СпрОбъект.Обменданными.Загрузка = Истина;
		СпрОбъект.Записать();
		
		Запрос.УстановитьПараметр("Объект", Элемент.Оборудование);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			МЗ = РегистрыСведений.ЗначенияРеквизитовПаспортов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МЗ, Выборка);
			МЗ.Удалить();
		КонецЦикла;
		
		ВыборкаИсточник.Сбросить();
		Пока ВыборкаИсточник.Следующий() Цикл
			МЗ = РегистрыСведений.ЗначенияРеквизитовПаспортов.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(МЗ, ВыборкаИсточник);
			МЗ.Объект = Элемент.Оборудование;
			МЗ.Записать();
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьНаСервере();
КонецПроцедуры
