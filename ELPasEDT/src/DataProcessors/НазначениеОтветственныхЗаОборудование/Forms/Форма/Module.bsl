
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СКД = РеквизитФормыВЗначение("Объект").ПолучитьМакет("СхемаКомпоновкиДанных");
	Адрес = ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Адрес));
	КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	
КонецПроцедуры

&НаСервере
Процедура ОтобратьНаСервере()
	
	Оборудование.Очистить();
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Оборудование.Загрузить(Обработка.ПолучитьОборудование(КомпоновщикНастроек.ПолучитьНастройки()));
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура Отобрать(Команда)
	ОтобратьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОборудованиеСнятьПометки(Команда)
	
	Для Каждого Элемент Из Оборудование Цикл
		Элемент.Выбран = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборудованиеУстановитьПометки(Команда)
	
	Для Каждого Элемент Из Оборудование Цикл
		Элемент.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НазначитьНаСервере()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРегистра = ?(ОтветственныйРостехнадзора, "ОборудованиеОтветственныеЛицаРостехнадзора", "ОборудованиеОтветственныеЛица");
	
	Для Каждого СтрТаб Из Оборудование Цикл
		
		Если Не СтрТаб.Выбран Тогда
			Продолжить;
		КонецЕсли;
		
		МЗ = РегистрыСведений[ИмяРегистра].СоздатьМенеджерЗаписи();
		МЗ.Период        = Период;
		МЗ.Оборудование  = СтрТаб.ЕдиницаОборудования;
		МЗ.Ответственный = Ответственный;
		МЗ.НомерПриказа  = НомерПриказа;
		МЗ.ДатаПриказа   = ДатаПриказа;
		
		МЗ.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Назначить(Команда)
	
	НазначитьНаСервере();
	
КонецПроцедуры


