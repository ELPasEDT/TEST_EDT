
Процедура УстановитьПартиюВИспользованииНаСервере(Партия, Дата, ВидРеагента, Отказ) Экспорт
	
	Если ЗначениеЗаполнено(Партия.ДатаВходящегоДокумента) И (НачалоДня(Дата) < НачалоДня(Партия.ДатаВходящегоДокумента)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Дата начала использования партии не может быть меньше даты приходной накладной. Партия не установлена",,,, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Удалим все данные по этому виду реагента после установленной даты,
	// а также данные по данной партии.
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПартииРеагентовВИспользовании.Период КАК Период,
	|	ПартииРеагентовВИспользовании.ВидРеагента КАК ВидРеагента,
	|	ПартииРеагентовВИспользовании.Подразделение КАК Подразделение
	|ИЗ
	|	РегистрСведений.ПартииРеагентовВИспользовании КАК ПартииРеагентовВИспользовании
	|ГДЕ
	|	(ПартииРеагентовВИспользовании.Период > &Период
	|				И ПартииРеагентовВИспользовании.ВидРеагента = &ВидРеагента
	|				И ПартииРеагентовВИспользовании.Подразделение = &Подразделение
	|			ИЛИ ПартииРеагентовВИспользовании.Партия = &Партия)");
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("ВидРеагента", ВидРеагента);
	Запрос.УстановитьПараметр("Подразделение", Партия.Подразделение);
	Запрос.УстановитьПараметр("Партия", Партия);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МЗ = РегистрыСведений.ПартииРеагентовВИспользовании.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МЗ, Выборка);
		МЗ.Удалить();
	КонецЦикла;
	
	
	МЗ = РегистрыСведений.ПартииРеагентовВИспользовании.СоздатьМенеджерЗаписи();
	МЗ.Период        = Дата;
	МЗ.ВидРеагента   = ВидРеагента;
	МЗ.Партия        = Партия;
	МЗ.Подразделение = Партия.Подразделение;
	МЗ.Записать();
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("НомерВходящегоДокумента");
	Поля.Добавить("ДатаВходящегоДокумента");
	Поля.Добавить("Концентрация");
	Поля.Добавить("Плотность");
	Поля.Добавить("ЕдиницаИзмеренияКонцентрации");
	Поля.Добавить("ЕдиницаИзмеренияПлотности");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Шаблон = "Парт. №%1 от %2, конц. %3%4";
	
	Номер = Данные.Номер;
	Дата = Данные.Дата;
	
	ПлотностьСтрока = "";
	
	Если ЗначениеЗаполнено(Данные.ЕдиницаИзмеренияПлотности) Тогда
		ПлотностьСтрока = ", плотн. " + Данные.Плотность + " " + Данные.ЕдиницаИзмеренияПлотности;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Данные.НомерВходящегоДокумента) Тогда
		Номер = Данные.НомерВходящегоДокумента;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Данные.ДатаВходящегоДокумента) Тогда
		Дата = Данные.ДатаВходящегоДокумента;
	КонецЕсли;
	
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Номер),
		Формат(Дата, "ДЛФ=D"),
		Строка(Данные.Концентрация) + " " + Данные.ЕдиницаИзмеренияКонцентрации,
		ПлотностьСтрока);
	
КонецПроцедуры
