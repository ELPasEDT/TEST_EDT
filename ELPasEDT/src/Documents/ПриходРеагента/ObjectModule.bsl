
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр РеагентыВПодразделениях Приход
	Движения.РеагентыВПодразделениях.Записывать = Истина;
	Движение = Движения.РеагентыВПодразделениях.Добавить();
	Движение.Период        = Дата;
	Движение.ВидДвижения   = ВидДвиженияНакопления.Приход;
	
	Движение.Подразделение = Подразделение;
	Движение.ВидРеагента   = ВидРеагента;
	Движение.Партия        = Ссылка;
	Движение.Реагент       = Реагент;
	
	Движение.Количество                = Количество;
	Движение.КоличествоВБазовойЕдинице = Количество * ЕдиницаИзмеренияКоличества.Коэффициент;	
	Движение.КоличествоМоногидратаВБазовойЕдинице = Движение.КоличествоВБазовойЕдинице * Концентрация / 100
		* ЕдиницаИзмеренияКонцентрации.Коэффициент;
	
	
	Если УчитыватьПлотностьПриРасчетеКоличества 
		И ЗначениеЗаполнено(Плотность) 
		И ЗначениеЗаполнено(ЕдиницаИзмеренияПлотности)
		И ЗначениеЗаполнено(ЕдиницаИзмеренияПлотности.Коэффициент) 
		Тогда
		
		Движение.КоличествоВБазовойЕдинице = Движение.КоличествоВБазовойЕдинице 
			* Плотность * ЕдиницаИзмеренияПлотности.Коэффициент;
			
		Движение.КоличествоМоногидратаВБазовойЕдинице = Движение.КоличествоМоногидратаВБазовойЕдинице
			* Плотность * ЕдиницаИзмеренияПлотности.Коэффициент;
			
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	Ответственный = ПользователиКлиентСервер.ТекущийПользователь();
	Подразделение = ПараметрыСеанса.ТекущийПользователь.Подразделение;
	Организация   = Подразделение.Организация;
КонецПроцедуры
